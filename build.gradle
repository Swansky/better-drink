plugins {
    id 'java'
    id 'maven-publish'
}

group = 'fr.swansky'
version = '1.0.3'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
}

test {
    useJUnitPlatform()
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/swansky/better-drink")
            credentials {
                username = project.findProperty("github.name") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("github.token") ?: System.getenv("PUB_GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenPublish(MavenPublication) {
            from(components.java)
            pom {
                name = 'Better Drink'
                description = 'Continuity of Drink project, a simple command system for minecraft server'
                url = 'https://github.com/Swansky/better-drink'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'Swansky'
                        name = 'Swansky'
                        email = 'pro@swansky.fr'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Swansky/better-drink.git'
                    developerConnection = 'scm:git:ssh://github.com/Swansky/better-drink.git'
                    url = 'https://github.com/swansky/better-drink'
                }
            }
        }
    }
}

